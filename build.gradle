buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
        classpath 'org.springframework:springloaded:1.2.0.RELEASE'
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'docker'

//group = 'com.gamarlos'
group = 'mcamacho'
//version = '0.0.1-SNAPSHOT'
version = '0.0.1'
jar {
    baseName = 'melapelapps'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-milestone" }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.0.7.RELEASE'
    }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
    all*.exclude module: "log4j-over-slf4j"
    all*.exclude module: "slf4j-log4j12"
    //all*.exclude module: "spring-boot-starter-logging"
}

dependencies {
    compile "org.springframework.cloud:spring-cloud-starter-hystrix"
    compile 'org.springframework.cloud:spring-cloud-starter-hystrix-dashboard'
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    // classpath "org.springframework.boot:spring-boot-starter-security"
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    //compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.7.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile "org.springframework.boot:spring-boot-starter-jetty:1.2.5.RELEASE"
    compile "org.codehaus.groovy:groovy:2.4.4"
    compile "org.codehaus.groovy:groovy-all:2.4.4"
    compile "org.apache.kafka:kafka_2.10:0.8.2.1"
    //compile "org.springframework.integration:spring-integration-kafka:1.1.1.RELEASE"
    //compile "org.springframework.integration:spring-integration-java-dsl:1.1.0.M2"
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.netflix.feign:feign-gson:8.10.0'
    compile 'com.netflix.feign:feign-slf4j:8.10.0'

    //compile 'org.slf4j:slf4j-api:1.7.12'
    //compile 'ch.qos.logback:logback-classic:1.1.3'
    testCompile "cglib:cglib-nodep:3.1"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile 'org.springframework:spring-test:4.1.7.RELEASE'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
}

/** Task testIntegration: creates task and tells gradle to recognize test_integration dir as test */
sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    test_integration {
        groovy {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    test_integrationCompile.extendsFrom testCompile
    test_integrationRuntime.extendsFrom testRuntime
}

task testIntegration(type:Test) {
    description = "Run integration tests (located in src/test_integration/...)."
    testClassesDir = sourceSets.test_integration.output.classesDir
    classpath = sourceSets.test_integration.runtimeClasspath
}
/** Task testIntegration: end */

task dockerLogin(type: Exec) {
    commandLine "bash", "-c", "docker login --username mcamacho --password xxx --email marcovmx07@gmail.com"
}

task buildDocker(type: Docker, dependsOn: [build, dockerLogin]) {
    push = true
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
            rename ('.*.jar', jar.baseName + '.jar')
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}